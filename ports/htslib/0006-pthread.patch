diff --git a/configure.ac b/configure.ac
index 49f2cbc7..3a205975 100644
--- a/configure.ac
+++ b/configure.ac
@@ -29,6 +29,7 @@ AC_PREREQ(2.63)  dnl This version introduced 4-argument AC_CHECK_HEADER
 AC_CONFIG_SRCDIR(hts.c)
 AC_CONFIG_HEADERS(config.h)
 
+m4_include([m4/ax_search_libs_rev.m4])
 m4_include([m4/hts_prog_cc_warnings.m4])
 m4_include([m4/hts_check_compile_flags_needed.m4])
 m4_include([m4/hts_hide_dynamic_syms.m4])
@@ -156,9 +157,11 @@ m4_ifdef([PKG_PROG_PKG_CONFIG], [PKG_PROG_PKG_CONFIG], [PKG_CONFIG=""])
 need_crypto=no
 pc_requires=
 static_LDFLAGS=$LDFLAGS
-static_LIBS='-lpthread -lz -lm'
+static_LIBS='-lz -lm'
 private_LIBS=$LDFLAGS
 
+AX_SEARCH_LIBS_REV([pthread_join], [thr pthread pthreadVC3 pthreadVC3d])
+
 AC_ARG_ENABLE([versioned-symbols],
   [AS_HELP_STRING([--disable-versioned-symbols],
                   [disable versioned symbols in shared library])],
diff --git a/m4/ax_search_libs_rev.m4 b/m4/ax_search_libs_rev.m4
new file mode 100644
index 00000000..7c307676
--- /dev/null
+++ b/m4/ax_search_libs_rev.m4
@@ -0,0 +1,42 @@
+# The idea is the same used as in AC_SEARCH_LIB, but unlike that the
+# no-library case is the last scenario instead of the first one.
+# The reason for this is to handle scenarios where the external library
+# is preferred over the internal C library implementation.  An example of
+# this is FreeBSD's pthread functionality, which has stub functions in the C
+# library that do not work.  See htscodecs issue#64
+
+# AX_SEARCH_LIBS_REV(FUNCTION, SEARCH-LIB-PATH
+AC_DEFUN([AX_SEARCH_LIBS_REV],
+[
+dnl Create an input to test linking a file calling $1.
+dnl Used by AC_LINK_IFELSE.
+AC_LANG_CONFTEST([AC_LANG_CALL([], [$1])])
+_found=no
+_LIBS=$LIBS
+AC_MSG_CHECKING([for function $1])
+for xlib in $2 ""
+do
+    if test "x$xlib" != "x"
+    then
+       LIBS="-l$xlib $_LIBS"
+       _res="-l$xlib"
+    else
+       LIBS="$_LIBS"
+       _res="(no library needed)"
+    fi
+
+    AC_LINK_IFELSE([], [_found=yes; break])
+done
+
+if test "$_found" = "yes"
+then
+    AC_MSG_RESULT([$_res])
+else
+    AC_MSG_RESULT([not found])
+    AC_MSG_ERROR([Function $1 not found])
+fi
+
+unset _found
+unset _res
+unset _LIBS
+])
